plugins {
    id 'java'
    id 'com.github.ben-manes.versions' version '0.51.0'
}

group 'ovh.akio'
version '1.4.1'

repositories {
    mavenCentral()
}

jar {
    manifest {
        attributes "Main-Class": "ovh.akio.hmu.Application"
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.11.3'

    implementation group: 'info.picocli', name: 'picocli', version: '4.7.6'
    implementation group: 'me.tongfei', name: 'progressbar', version: '0.10.1'

    implementation group: 'net.sf.sevenzipjbinding', name: 'sevenzipjbinding', version: '16.02-2.01'
    implementation group: 'net.sf.sevenzipjbinding', name: 'sevenzipjbinding-all-platforms', version: '16.02-2.01'

}

test {
    useJUnitPlatform()
}

static def isNonStable(String version) {
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

dependencyUpdates {
    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
}
